plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.13'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    // Selenium
    implementation 'org.seleniumhq.selenium:selenium-java:4.20.0'
    // WebDriverManager
    implementation 'io.github.bonigarcia:webdrivermanager:5.8.0'
    // DI
    implementation 'com.google.inject:guice:7.0.0'
    // HTML parsing
    implementation 'org.jsoup:jsoup:1.17.2'

    // JUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'

    // Cucumber BDD
    testImplementation 'io.cucumber:cucumber-java:7.22.1'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.22.1'
    testImplementation 'io.cucumber:cucumber-picocontainer:7.22.1'
}


tasks.withType(Test).configureEach {
    useJUnitPlatform {
        // Запускать только Cucumber-движок
        includeEngines 'cucumber'
    }

    // Принудительно устанавливаем кодировку UTF-8 в JVM и тестовой платформе
    jvmArgs '-Dfile.encoding=UTF-8'
    systemProperty 'file.encoding', 'UTF-8'
}

checkstyle {
    configFile = file("linters/checkstyle.xml")
    toolVersion = "10.12.3"
}

spotbugs {
    toolVersion = "4.8.3"
    effort = 'max'
    reportLevel = 'low'
    excludeFilter = file("linters/spotbugs-exclude.xml")
}