plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.13'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    implementation 'org.seleniumhq.selenium:selenium-java:4.20.0'
    implementation 'io.github.bonigarcia:webdrivermanager:5.8.0'
    implementation 'com.google.inject:guice:7.0.0'
    implementation 'org.jsoup:jsoup:1.17.2'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'io.cucumber:cucumber-java:7.22.1'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.22.1'
    testImplementation 'io.cucumber:cucumber-picocontainer:7.22.1'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

// Отключаем стандартный тест-таск
tasks.named('test').configure {
    enabled = false
}

// Явная таска для запуска только Cucumber–BDD
 task cucumber(type: Test) {
     group = 'verification'
     description = 'Запустить только Cucumber BDD сценарии'
     useJUnitPlatform {
         includeEngines 'cucumber'
     }
     systemProperty 'browser', project.findProperty('browser') ?: 'chrome'
     jvmArgs '-Dfile.encoding=UTF-8'
     systemProperty 'file.encoding', 'UTF-8'
     testClassesDirs = sourceSets.test.output.classesDirs
     classpath = sourceSets.test.runtimeClasspath
 }

check.dependsOn cucumber

checkstyle {
    configFile = file("linters/checkstyle.xml")
    toolVersion = "10.12.3"
}

spotbugs {
    toolVersion = "4.8.3"
    effort = 'max'
    reportLevel = 'low'
    excludeFilter = file("linters/spotbugs-exclude.xml")
}